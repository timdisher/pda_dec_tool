model{

# this controls for studies with one outcome not reported by setting the correlation equal to 
# zero

for (k in 1:(2*Ns-N2h)){control[k]<-step(9999-varr[k])*step(9999-vard[k])}

# two-arm studies

for( i in 1:N2h){
s[i,1,1]<-varr[i]+psiR.sq
s[i,2,2]<-vard[i]+psiD.sq
s[i,1,2]<-control[i]*rho1*sqrt(varr[i]+psiR.sq)*sqrt(vard[i]+psiD.sq)
s[i,2,1]<-s[i,1,2]
prec2A[i,1:2,1:2]<-inverse(s[i,,])
y[(2*i-1):(2*i)]~dmnorm(mean[(2*i-1):(2*i)],prec2A[i,,])}


# Parameterization of the means

for(i in 1:N2h) {                   
	mean[2*i-1] <- -dR[T2[i]] + dR[T1[i]]
mean[2*i] <- -dD[T2[i]]+ dD[T1[i]]
}


# Priors		

for(k in 1:(ref-1)) {	dR[k] ~ dnorm(0,.01)}
for(k in (ref+1):NT) {dR[k] ~ dnorm(0,.01)}		
for(k in 1:(ref-1)) {	dD[k] ~ dnorm(0,.01)}
for(k in (ref+1):NT) {dD[k] ~ dnorm(0,.01)}		
psiR.sq<-psi1*psi1

psi1~dunif(0,1)
psiD.sq<-psi2*psi2
psi2~dunif(0,1)
rho1~dunif(-1,0)

#Estimated  Effect Sizes

dR[ref]<- 0
for (c in 1:(ref-1)) { Eff.refR[c]<- exp(dR[c] - dR[ref] )} 
for (c in (ref+1):NT) { Eff.refR[c]<- exp(dR[c] - dR[ref] )	} 
for (c in 1:(NT-1)) {
	for (k in (c+1):NT) { 
EffR[c,k] <- exp(dR[k] - dR[c])}}
dD[ref]<- 0
for (c in 1:(ref-1)) { Eff.refD[c]<- exp(dD[c] - dD[ref] )} 
for (c in (ref+1):NT) { Eff.refD[c]<- exp(dD[c] - dD[ref] )	} 
for (c in 1:(NT-1)) {  
	for (k in (c+1):NT) { 
		EffD[c,k] <- exp(dD[k] - dD[c]) }}	
  
}